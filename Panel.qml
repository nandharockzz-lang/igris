import QtQuick
import QtQuick.Controls
import Quickshell
import Quickshell.Io
import qs.Commons
import qs.Ui

// Settings panel for the voice assistant.
//
// Jarvis's settings live in ~/.config/jarvis/config.toml, because the daemon
// that reads them is Python and knows nothing about the bar. QML has no TOML
// support, so every read and write goes through the `jarvis-config` helper:
// `show` hands back JSON, `set` rewrites a single line and keeps the comments.
//
// That helper also decides what is settable. It refuses to touch anything
// under [agents.*], so nothing here can rewrite the argv the daemon executes.
Panel {
  id: root
  moduleName: "dorian.voice"
  ipcTarget: "dorian.voice.panel"
  manageIpc: false

  property var anchorItem: null
  property var hostWidget: null
  readonly property var host: hostWidget || root

  readonly property color fg: bar ? bar.foreground : Color.foreground
  readonly property color accent: Color.accent
  readonly property string fontFamily: bar ? bar.fontFamily : Style.font.family

  readonly property bool armed: host && host.armed === true
  readonly property string pipeline: host ? host.pipeline : "off"
  // Live UI mirrors of the widget (daemon-published, same source as the bar).
  // Read off hostWidget directly: `host` falls back to this panel itself
  // before injection, which self-references these same-named properties
  // into binding loops.
  readonly property string avatarState: hostWidget && hostWidget.avatarState ? hostWidget.avatarState : "off"
  readonly property string modeName: hostWidget && hostWidget.modeName ? hostWidget.modeName : "Safe"
  readonly property int remainingSecs: hostWidget && hostWidget.remainingSecs ? hostWidget.remainingSecs : 0
  readonly property real voiceLevel: hostWidget && hostWidget.voiceLevel ? hostWidget.voiceLevel : 0
  readonly property var micBars: hostWidget && hostWidget.micBars ? hostWidget.micBars : []
  readonly property string toolCat: hostWidget && hostWidget.toolCat ? hostWidget.toolCat : ""
  readonly property bool toolFresh: hostWidget ? hostWidget.toolFresh === true : false
  readonly property bool blink: hostWidget ? hostWidget.blink === true : false
  readonly property bool reduceMotion: hostWidget ? hostWidget.reduceMotion === true : false
  readonly property bool highContrast: hostWidget ? hostWidget.highContrast === true : false
  readonly property string liveMode: hostWidget && hostWidget.mode ? hostWidget.mode : "safe"

  // Mirrors of config.toml, refilled by `jarvis-config show` every time the
  // panel opens. Empty until the first load lands.
  property string agent: ""
  property string mode: "safe"
  property var modes: []
  property real autoDisarm: 300
property string wakeWord: ""
   property var agents: []
   property var wakeWords: []
   property var voices: []
   property var availableModels: []  // Available OpenCode models
   property real wakeThreshold: 0.5
   property real silenceTail: 1.2
   property real maxCommand: 15.0
   property string voice: ""
   property string model: ""  // Top-level model setting for OpenCode agent
   // Listener diagnostics (daemon-published): active speech engine/model,
   // configured wake word, and the last startup refusal, if any.
   property string sttEngine: ""
   property string sttModel: ""
   property string startupError: ""
   property bool loaded: false
   property string errorText: ""

  // A voice is ~63MB, so selecting one that is not on disk yet downloads it
  // first. The dropdown locks while that runs.
  property bool installingVoice: false
  property string pendingVoice: ""

  // The daemon reads its config once at startup, so a change only takes
  // effect on restart. Restarting a disarmed listener would arm it, which is
  // never what someone wants from a settings panel, so it stays disarmed and
  // the pending note explains why.
  property bool pendingRestart: false

  // Arming starts the daemon, which reads the file as it comes up, so the
  // listener is current by definition and the notice has nothing left to
  // warn about. This is what resolves the disarmed case the notice describes.
  onArmedChanged: if (root.armed) root.pendingRestart = false

  function open()   { load(); root.controller.show() }
  function close()  { root.controller.hide() }
  function toggle() { root.opened ? root.close() : root.open() }

  function load() {
    errorText = ""
    showProc.running = true
  }

  function agentEntry(name) {
    for (var i = 0; i < agents.length; i++)
      if (agents[i].name === name) return agents[i]
    return null
  }

  // `actions` governs Jarvis's own directive broker, NOT whether the agent
  // CLI has tools of its own. Deriving "answer-only" from it once let a
  // read-only Codex sandbox, which could read the whole home directory,
  // caption itself as answer-only right here. The daemon reports what it can
  // actually verify in `tools`; anything it does not recognise is FAIL and
  // says so -- never displayed as safe.
  readonly property string agentNote: {
    var e = agentEntry(agent)
    if (!e) return ""
    if (!e.installed) return "not installed, replies will fail"
    if (e.tools === "granted") return "FAIL: this agent has CLI tools of its own"
    if (e.tools !== "denied")
      return "FAIL: tools not verified -- run the canary before trusting it"
    return e.actions ? "can act (workspace broker verbs)" : "answer-only"
  }

  // Mode note: Safe answers only; Basic (workspace) adds broker verbs on an
  // explicit arm window; Full (privileged) adds sandboxed tools with a short
  // window. Voice never authorizes destructive ops in any mode.
  readonly property string modeNote: {
    if (root.mode === "privileged")
      return "Full: sandboxed full-tools on a short window; destructive ops need button confirmation"
    if (root.mode === "workspace")
      return "Basic: answers plus broker verbs (volume, apps, workspaces) on an arm window; no shell"
    return "Safe: always-on allowed; answers only, never acts"
  }

  readonly property string countdownText: {
    if (!root.armed || root.liveMode === "safe" || !(root.remainingSecs > 0))
      return ""
    var m = Math.floor(root.remainingSecs / 60)
    var s = root.remainingSecs % 60
    return "disarms in " + m + ":" + (s < 10 ? "0" + s : s)
  }

  // Hero and badge read the widget's daemon-published state (same source as
  // the bar), so panel and daemon always agree. The dropdown below edits the
  // config request; an armed change restarts the daemon into it at once.

  // config.toml stores a filename; the dropdown works in catalog ids.
  readonly property string voiceId: root.voice.replace(/\.onnx$/, "")

  function voiceEntry(id) {
    for (var i = 0; i < voices.length; i++)
      if (voices[i].id === id) return voices[i]
    return null
  }

  function chooseVoice(id) {
    if (id === root.voiceId || root.installingVoice) return
    var entry = voiceEntry(id)
    if (entry && entry.installed) {
      root.apply("voice", entry.file)
      return
    }
    root.errorText = ""
    root.pendingVoice = id
    root.installingVoice = true
    installProc.command = [root.helper, "install-voice", id]
    installProc.running = true
  }

  // One place for every write, so the restart bookkeeping cannot drift.
  // While armed, writes go through `apply`: validate all, write once, a
  // single restart, readiness-awaited, rolled back on failure. Disarmed,
  // a plain `set` (the next arm reads the file anyway).
  function apply(key, value) {
    errorText = ""
    if (root.armed)
      setProc.command = [root.helper, "apply", key + "=" + String(value),
                         "--restart-unit", "jarvis", "--wait-ready", "25"]
    else
      setProc.command = [root.helper, "set", key, String(value)]
    setProc.running = true
  }

  // Restart with the same guarantees: readiness-awaited, and a refusal
  // surfaces instead of leaving a stale listener running.
  function restartNow() {
    errorText = ""
    root.pendingRestart = false
    setProc.command = [root.helper, "apply",
                       "--restart-unit", "jarvis", "--wait-ready", "25"]
    setProc.running = true
  }

  // setProc exiting 0 means the write landed -- and, when armed, that
  // `apply` already restarted exactly once and awaited readiness. So an
  // armed success only reloads and clears the notice; a disarmed success
  // waits for the next arm. Failures reload (apply rolled back) and keep
  // the notice state honest via the setProc error path.
  function onApplied() {
    load()
    if (armed) {
      pendingRestart = false
    } else {
      pendingRestart = true
    }
  }

  Process {
    id: showProc
command: [root.helper, "show"]
   stdout: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        try {
          var d = JSON.parse(String(text || "{}"))
          root.agent = d.agent || ""
          root.mode = d.mode || "safe"
          root.modes = d.modes || []
          root.wakeWord = d.wake_word || ""
          root.model = d.model || ""  // Load the model setting
          root.availableModels = d.available_models || []  // Load available models
          if (d.stt) {
            root.sttEngine = d.stt.engine || ""
            root.sttModel = d.stt.model || ""
          }
          root.startupError = d.startup_error || ""
          root.agents = d.agents || []
          root.wakeWords = d.wake_words || []
          root.voices = d.voices || []
          root.voice = d.voice || ""
          if (d.workspace) {
            root.autoDisarm = d.workspace.auto_disarm_seconds
          }
          if (d.listen) {
            root.wakeThreshold = d.listen.wake_threshold
            root.silenceTail = d.listen.silence_tail
            root.maxCommand = d.listen.max_command
          }
          root.loaded = true
        } catch (e) {
          root.errorText = "Could not read the config file."
        }
      }
    }
    onExited: function(code) {
      if (code !== 0) root.errorText = "jarvis-config failed (exit " + code + ")"
    }
  }

  Process {
    id: setProc
    // jarvis-config reports refusals on stderr; surface them rather than
    // silently leaving a control showing a value that was never written.
    stderr: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var msg = String(text || "").replace(/^\[jarvis\].*$/gm, "").trim()
        if (msg) root.errorText = msg
      }
    }
    onExited: function(code) {
      if (code === 0) root.onApplied()
      else { if (!root.errorText) root.errorText = "Could not save that setting."; root.load() }
    }
  }

  Process {
    id: installProc
    stderr: StdioCollector {
      waitForEnd: true
      onStreamFinished: {
        var msg = String(text || "").replace(/^\[jarvis\].*$/gm, "").trim()
        if (msg) root.errorText = msg
      }
    }
    onExited: function(code) {
      root.installingVoice = false
      if (code === 0) {
        root.apply("voice", root.pendingVoice + ".onnx")
      } else if (!root.errorText) {
        root.errorText = "Could not download that voice."
      }
      root.pendingVoice = ""
    }
  }

  // The daemon reads config.toml once, at startup. Anything that edits the
  // file behind it -- the Edit config button, a text editor, another machine
  // syncing -- leaves the listener running settings that no longer match
  // what the file says, with nothing on screen to say so. Watch the file and
  // say so.
  //
  // No guard is needed against our own writes: `apply` while armed already
  // restarts exactly once and clears the notice on success.
  FileView {
    path: root.configPath
    watchChanges: true
    printErrors: false
    onFileChanged: {
      reload()
      if (root.armed) root.pendingRestart = true
    }
  }

  KeyboardPanel {
    id: panel
    anchorItem: root.anchorItem
    owner: root.host
    bar: root.bar
    open: root.opened
    centerOnBar: false
    focusTarget: keys
    contentWidth: panel.fittedContentWidth(Style.space(380))
    // The cap is what the card is allowed to grow to, not what the content
    // needs. At 560 the hero, the three pickers, the three sliders and the
    // buttons together overran it, and everything past the cap was simply
    // clipped -- the buttons were unreachable. This fits the lot on a normal
    // screen; fittedContentHeight still clamps to the space the bar leaves,
    // and the Flickable below makes whatever a short screen cuts scrollable
    // rather than lost.
    contentHeight: panel.fittedContentHeight(column.implicitHeight, Style.space(760))

    PanelKeyCatcher {
      id: keys
      anchors.fill: parent
      onCloseRequested: root.close()
      onTabRequested: function(direction) { root.switchPanel(direction) }
      onActivateRequested: if (root.host && root.host.toggle) root.host.toggle()
      onMoveRequested: function(dx, dy) {
        if (dy === 0) return
        var maxY = Math.max(0, panelFlick.contentHeight - panelFlick.height)
        panelFlick.contentY = Math.max(0, Math.min(maxY,
                                panelFlick.contentY + dy * Style.space(56)))
      }

      Flickable {
        id: panelFlick
        anchors.fill: parent
        contentWidth: width
        contentHeight: column.implicitHeight
        clip: true
        boundsBehavior: Flickable.StopAtBounds
        flickableDirection: Flickable.VerticalFlick
        interactive: contentHeight > height
        ScrollBar.vertical: ScrollBar { policy: ScrollBar.AsNeeded }

        // A panel always opens at the top. Without this the Flickable keeps
        // whatever contentY it was left at, and a reopen starts mid-card
        // with the hero scrolled off.
        Connections {
          target: root
          function onOpenedChanged() { if (root.opened) panelFlick.contentY = 0 }
        }

        Column {
          id: column
          width: panelFlick.width
          spacing: Style.space(14)

          // ------------------------------------------------------------ hero
          Item {
            width: parent.width
            implicitHeight: Math.max(heroAvatar.implicitHeight, heroText.implicitHeight,
                                     heroSwitch.implicitHeight)

            // Tap the avatar to disarm (a physical tap is the action). Tapping
            // while disarmed does nothing -- arming stays on the switch, which
            // steps the Basic/Full toggle.
            Avatar {
              id: heroAvatar
              side: 52
              state: root.avatarState
              mode: root.liveMode
              level: root.voiceLevel
              blink: root.blink
              bob: 0
              reduceMotion: root.reduceMotion
              highContrast: root.highContrast
              anchors.left: parent.left
              anchors.verticalCenter: parent.verticalCenter

              MouseArea {
                id: avatarMouse
                anchors.fill: parent
                hoverEnabled: true
                cursorShape: root.armed ? Qt.PointingHandCursor : Qt.ArrowCursor
                onClicked: {
                  if (root.armed && root.host && root.host.stopNow)
                    root.host.stopNow()
                }
              }
              PanelToolTip {
                visible: avatarMouse.containsMouse
                text: root.armed ? "Tap to disarm the microphone"
                                 : "Microphone is off"
                fontFamily: root.fontFamily
              }
            }

            Column {
              id: heroText
              anchors.left: heroAvatar.right
              anchors.leftMargin: Style.space(12)
              anchors.right: heroSwitch.left
              anchors.rightMargin: Style.space(12)
              anchors.verticalCenter: parent.verticalCenter
              spacing: Style.space(2)

              Text {
                text: "Voice Assistant"
                color: root.fg
                font.family: root.fontFamily
                font.pixelSize: Style.font.title
                font.bold: true
                elide: Text.ElideRight
                width: parent.width
              }
              Text {
                text: root.host ? root.host.stateLabel : ""
                color: root.armed ? root.accent : Qt.darker(root.fg, 1.4)
                font.family: root.fontFamily
                font.pixelSize: Style.font.caption
                elide: Text.ElideRight
                width: parent.width
              }
              // Mode badge + countdown + tool category (category only).
              Text {
                text: {
                  if (!root.armed) return "mic off"
                  var t = root.modeName + " mode"
                  if (root.countdownText !== "") t += " · " + root.countdownText
                  if (root.toolFresh && root.toolCat !== "")
                    t += " · " + root.toolCat
                  return t
                }
                color: root.liveMode === "privileged" ? "#f0883e"
                  : root.liveMode === "workspace" ? "#3fb950"
                  : Qt.darker(root.fg, 1.4)
                font.family: root.fontFamily
                font.pixelSize: Style.font.caption
                font.bold: true
                elide: Text.ElideRight
                width: parent.width
              }
            }

            ToggleSwitch {
              id: heroSwitch
              checked: root.armed
              foreground: root.fg
              accent: root.accent
              anchors.right: parent.right
              anchors.verticalCenter: parent.verticalCenter
              onToggled: if (root.host && root.host.toggle) root.host.toggle()

              PanelToolTip {
                visible: heroSwitch.containsMouse
                text: root.armed ? "Step Basic/Full (tap avatar to disarm)"
                                 : "Arm Basic mode"
                fontFamily: root.fontFamily
              }
            }
          }

          // Voice-reception EQ: live gated mic buckets, dimmed the moment
          // listening ends. Transient amplitudes only -- nothing stored.
          Column {
            width: parent.width
            spacing: Style.space(4)
            visible: root.pipeline === "listening"

            EqBars {
              bars: root.micBars
              active: root.pipeline === "listening"
              reduceMotion: root.reduceMotion
              highContrast: root.highContrast
              foreground: Qt.darker(root.fg, 1.4)
              accent: root.accent
              maxHeight: 32
            }
            Text {
              text: root.pipeline === "listening" && root.micBars.length === 0
                ? "listening…"
                : "live mic level (not recorded)"
              color: Qt.darker(root.fg, 1.4)
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
            }
          }

          // Only shown when it matters: a change the daemon has not read.
          // Disarmed, that resolves itself on the next arm; armed, it takes
          // a restart, and the button for it is at the bottom of this panel.
          BorderSurface {
            width: parent.width
            visible: root.pendingRestart
            implicitHeight: pendingText.implicitHeight + Style.space(16)
            radius: Style.cornerRadius
            color: Style.normalFillFor(root.fg, root.accent)
            borderSpec: Border.controlSpec("normal", root.fg, root.accent)

            Text {
              id: pendingText
              anchors.centerIn: parent
              width: parent.width - Style.space(20)
              text: root.armed
                ? "The config file changed. Restart the listener to use it."
                : "Saved. Takes effect when you arm the listener."
              color: root.fg
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              wrapMode: Text.WordWrap
              horizontalAlignment: Text.AlignHCenter
            }
          }

          BorderSurface {
            width: parent.width
            visible: root.errorText !== ""
            implicitHeight: errText.implicitHeight + Style.space(16)
            radius: Style.cornerRadius
            color: Style.normalFillFor(root.fg, root.accent)
            borderSpec: Border.controlSpec("hover-cursor", root.fg, root.accent)

            Text {
              id: errText
              anchors.centerIn: parent
              width: parent.width - Style.space(20)
              text: root.errorText
              color: root.fg
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              wrapMode: Text.WordWrap
              horizontalAlignment: Text.AlignHCenter
            }
          }

          // Last startup refusal, daemon-published. A failed change keeps
          // the prior config (the write never lands), so this is the
          // reason the mic is off -- fix the setting, restart, and a good
          // start clears it.
          BorderSurface {
            width: parent.width
            visible: root.startupError !== ""
            implicitHeight: startupText.implicitHeight + Style.space(16)
            radius: Style.cornerRadius
            color: Style.normalFillFor(root.fg, root.accent)
            borderSpec: Border.controlSpec("hover-cursor", root.fg, root.accent)

            Text {
              id: startupText
              anchors.centerIn: parent
              width: parent.width - Style.space(20)
              text: "Listener failed to start: " + root.startupError
              color: root.fg
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              wrapMode: Text.WordWrap
              horizontalAlignment: Text.AlignHCenter
            }
          }

          PanelSeparator { foreground: root.fg }

          // -------------------------------------------------------- behaviour
          PanelSectionHeader {
            text: "ASSISTANT"
            foreground: root.fg
            fontFamily: root.fontFamily
          }

          Column {
            width: parent.width
            spacing: Style.space(4)

            Dropdown {
              width: parent.width
              label: "Agent"
              value: root.agent
              enabled: root.loaded
              foreground: root.fg
              accent: root.accent
              fontFamily: root.fontFamily
              options: {
                var out = []
                for (var i = 0; i < root.agents.length; i++) {
                  var a = root.agents[i]
                  out.push({
                    value: a.name,
                    label: a.name + (a.installed ? "" : "  (not installed)")
                  })
                }
                return out
              }
              onChanged: function(v) { if (v !== root.agent) root.apply("agent", v) }
            }

            Text {
              text: root.agentNote
              visible: text !== ""
              color: Qt.darker(root.fg, 1.4)
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
            }
          }

          Column {
            width: parent.width
            spacing: Style.space(4)

            Dropdown {
              width: parent.width
              label: "Mode"
              value: root.mode
              enabled: root.loaded
              foreground: root.fg
              accent: root.accent
              fontFamily: root.fontFamily
              // Friendly names over daemon modes: Safe=safe answers-only,
              // Basic=workspace broker verbs, Full=privileged sandbox.
              // Values stay daemon-internal; the daemon validates all.
              options: [
                { value: "safe", label: "Safe (answers only)" },
                { value: "workspace", label: "Basic (voice commands)" },
                { value: "privileged", label: "Full (sandboxed tools)" }
              ]
              onChanged: function(v) { if (v !== root.mode) root.apply("mode", v) }
            }

            Text {
              text: root.modeNote
                    + (root.countdownText !== "" ? " · " + root.countdownText : "")
              visible: text !== ""
              color: Qt.darker(root.fg, 1.4)
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              wrapMode: Text.WordWrap
              width: parent.width
            }
          }

          Dropdown {
            width: parent.width
            label: "Wake word"
            value: root.wakeWord
            enabled: root.loaded
            foreground: root.fg
            accent: root.accent
            fontFamily: root.fontFamily
            // Only installed models are offered: selecting a name with no
            // model file would brick the listener into a restart loop.
            // Entries may be objects {name, installed} (new) or plain
            // strings (old show output) -- both render.
            options: {
              var out = []
              for (var i = 0; i < root.wakeWords.length; i++) {
                var w = root.wakeWords[i]
                var name = (typeof w === "string") ? w : w.name
                var inst = (typeof w === "string") ? true : w.installed === true
                if (!inst) continue
                out.push({ value: name, label: name.replace(/_/g, " ") })
              }
              return out
            }
            onChanged: function(v) { if (v !== root.wakeWord) root.apply("wake_word", v) }
          }

          Column {
            width: parent.width
            spacing: Style.space(4)

            Dropdown {
              width: parent.width
              label: "Voice"
              value: root.voiceId
              enabled: root.loaded && !root.installingVoice
              foreground: root.fg
              accent: root.accent
              fontFamily: root.fontFamily
              options: {
                var out = []
                for (var i = 0; i < root.voices.length; i++) {
                  var v = root.voices[i]
                  out.push({
                    value: v.id,
                    label: v.id.replace(/^en_/, "").replace(/-medium$/, "").replace(/_/g, " ")
                           + (v.installed ? "" : "  (download)")
                  })
                }
                return out
              }
              onChanged: function(v) { root.chooseVoice(v) }
            }

            Text {
text: root.installingVoice
                   ? "Downloading " + root.pendingVoice + "… about 63 MB."
                   : "Voices not listed here work too. Put a path in the config file."
               color: Qt.darker(root.fg, 1.4)
               font.family: root.fontFamily
               font.pixelSize: Style.font.caption
               wrapMode: Text.WordWrap
               width: parent.width
             }
           }

           // Model dropdown - only shown for OpenCode agent
           Column {
             width: parent.width
             spacing: Style.space(4)

Dropdown {
                width: parent.width
                label: "Model"
                value: root.model
                enabled: root.loaded && (root.agent === "opencode-voice" || root.agent === "grok")
                foreground: root.fg
                accent: root.accent
                fontFamily: root.fontFamily
                options: {
                  var out = []
                  for (var i = 0; i < root.availableModels.length; i++) {
                    var model = root.availableModels[i]
                    // model is an object with id and name
                    var displayName = model.name.replace(/-/g, " ")
                    out.push({
                      value: model.id,
                      label: displayName
                    })
                  }
                  return out
                }
                onChanged: function(v) { if (v !== root.model) root.apply("model", v) }
              }

             Text {
               text: root.agent === "grok"
                     ? "Grok 4.6 is the current flagship. Requires the grok CLI (logged in at grok.com)."
                     : root.agent === "opencode-voice"
                     ? "Select the OpenCode model to use. Switch to a different free model when the current one is exhausted."
                     : "Model selection is available for the OpenCode and Grok agents."
               visible: text !== ""
               color: Qt.darker(root.fg, 1.4)
               font.family: root.fontFamily
               font.pixelSize: Style.font.caption
               wrapMode: Text.WordWrap
               width: parent.width
             }
           }

           PanelSeparator { foreground: root.fg }

          // --------------------------------------------------------- listening
          PanelSectionHeader {
            text: "LISTENING"
            foreground: root.fg
            fontFamily: root.fontFamily
          }

          Column {
            width: parent.width
            spacing: Style.space(6)

            Item {
              width: parent.width
              implicitHeight: sensLabel.implicitHeight

              Text {
                id: sensLabel
                text: "Sensitivity"
                color: root.fg
                font.family: root.fontFamily
                font.pixelSize: Style.font.bodySmall
                anchors.left: parent.left
              }
              Text {
                text: root.wakeThreshold.toFixed(2)
                color: Qt.darker(root.fg, 1.4)
                font.family: root.fontFamily
                font.pixelSize: Style.font.caption
                anchors.right: parent.right
              }
            }

            // Lower threshold = fires more easily, so the slider is inverted to
            // read left-to-right as "less sensitive" -> "more sensitive".
            PanelSlider {
              width: parent.width
              bar: root.bar
              enabled: root.loaded
              value: 1.0 - root.wakeThreshold
              minimum: 0.05
              maximum: 0.9
              step: 0.05
              onReleased: function(v) { root.apply("listen.wake_threshold", (1.0 - v).toFixed(2)) }
            }

            Text {
              text: "Higher picks up the wake word more readily, and misfires more."
              color: Qt.darker(root.fg, 1.4)
              font.family: root.fontFamily
              font.pixelSize: Style.font.caption
              wrapMode: Text.WordWrap
              width: parent.width
            }
          }

          Column {
            width: parent.width
            spacing: Style.space(6)

            Item {
              width: parent.width
              implicitHeight: tailLabel.implicitHeight

              Text {
                id: tailLabel
                text: "Pause before it answers"
                color: root.fg
                font.family: root.fontFamily
                font.pixelSize: Style.font.bodySmall
                anchors.left: parent.left
              }
              Text {
                text: root.silenceTail.toFixed(1) + "s"
                color: Qt.darker(root.fg, 1.4)
                font.family: root.fontFamily
                font.pixelSize: Style.font.caption
                anchors.right: parent.right
              }
            }

            PanelSlider {
              width: parent.width
              bar: root.bar
              enabled: root.loaded
              value: root.silenceTail
              minimum: 0.4
              maximum: 3.0
              step: 0.1
              onReleased: function(v) { root.apply("listen.silence_tail", v.toFixed(1)) }
            }
          }

          Column {
            width: parent.width
            spacing: Style.space(6)

            Item {
              width: parent.width
              implicitHeight: maxLabel.implicitHeight

              Text {
                id: maxLabel
                text: "Longest question"
                color: root.fg
                font.family: root.fontFamily
                font.pixelSize: Style.font.bodySmall
                anchors.left: parent.left
              }
              Text {
                text: Math.round(root.maxCommand) + "s"
                color: Qt.darker(root.fg, 1.4)
                font.family: root.fontFamily
                font.pixelSize: Style.font.caption
                anchors.right: parent.right
              }
            }

            PanelSlider {
              width: parent.width
              bar: root.bar
              enabled: root.loaded
              value: root.maxCommand
              minimum: 5
              maximum: 60
              step: 5
              integer: true
              onReleased: function(v) { root.apply("listen.max_command", Math.round(v)) }
            }
          }

          PanelSeparator { foreground: root.fg }

          Row {
            width: parent.width
            spacing: Style.space(8)

            Button {
              id: restartBtn
              width: Math.floor((parent.width - Style.space(8)) / 2)
              text: "Restart"
              iconText: "󰑖"
              bordered: true
              foreground: root.fg
              accent: root.accent
              fontFamily: root.fontFamily
              fontSize: Style.font.bodySmall
              onClicked: root.restartNow()
            }

            Button {
              width: parent.width - restartBtn.width - Style.space(8)
              text: "Edit config"
              iconText: "󰈙"
              bordered: true
              foreground: root.fg
              accent: root.accent
              fontFamily: root.fontFamily
              fontSize: Style.font.bodySmall
              // Not xdg-open: config.toml is text/plain, whose handler here
              // is a terminal editor with Terminal=true, and execDetached
              // gives it no terminal to run in. The result was an invisible
              // nvim per click, piling up unreachable in the background.
              // omarchy-launch-config-editor opens the user's chosen editor,
              // wrapping a TUI one in a terminal, and toasts what it opened.
              onClicked: {
                Quickshell.execDetached(
                  ["omarchy-launch-config-editor", root.configPath])
                root.close()
              }
            }
          }

          Text {
            width: parent.width
            text: (root.sttEngine !== "" && root.sttModel !== "")
              ? "Listener: " + root.sttEngine + "/" + root.sttModel
                + (root.wakeWord !== "" ? " · wake " + root.wakeWord.replace(/_/g, " ") : "")
                + "\nEverything else (adding an agent, the voice) lives in the config file."
              : "Everything else (adding an agent, the voice) lives in the config file."
            color: Qt.darker(root.fg, 1.4)
            font.family: root.fontFamily
            font.pixelSize: Style.font.caption
            wrapMode: Text.WordWrap
          }
        }
      }
    }
  }

  readonly property string configPath:
    (Quickshell.env("XDG_CONFIG_HOME") || (Quickshell.env("HOME") + "/.config"))
    + "/jarvis/config.toml"

  // Absolute, because omarchy-shell's PATH is not ours to assume and the
  // helper has to run under the venv's interpreter.
  readonly property string helper:
    Quickshell.env("HOME") + "/.local/share/jarvis/bin/jarvis-config"
}
